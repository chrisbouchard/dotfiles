#!/usr/bin/env zsh

setopt promptsubst
unsetopt transient_rprompt

function set_prompt {
    local a

    myjobs=()
    for a in ${(k)jobstates}
    do
        local j=$jobstates[$a]
        local i="${${(@s,:,)j}[2]}"

        myjobs+=($a${i//[^+-]/})
    done
}

# If I am using vi keys, I want to know what mode I'm currently using.
# zle-keymap-select is executed every time KEYMAP changes.
# From http://zshwiki.org/home/examples/zlewidgets
function zle-keymap-select {
    VIMODE="${${KEYMAP/vicmd/COMMAND}/(main|viins)/}"
    zle reset-prompt
}

function precmd {
    VIMODE=''
    TITLE=''

    if [[ "$TERM" = *screen* ]]
    then
        if [ -z "$SSH_CONNECTION" ]
        then
            TITLE=$'%{\ek%# %1~\e\\%}'
        else
            TITLE=$'%{\ek%# %m:%1~\e\\%}'
        fi
    fi

    set_prompt
}

function preexec {
    if [[ "$TERM" = *screen* ]]
    then
        local HOSTNAME="$(hostname)"
        local CMD=${2[(wr)^(*=*|sudo|exec|command|nocorrect|noglob|-*)]}

        if [ "$CMD" = 'fg' ] && [ -n "$jobstates" ]
        then
            CMD=$(jobs %+ | awk '{print $4}')
        fi

        if [ -z "$SSH_CONNECTION" ]
        then
            echo -ne "\ek$CMD\e\\"
        else
            echo -ne "\ek$HOSTNAME:$CMD\e\\"
        fi
    fi
}

zle -N zle-keymap-select

if [ ! -z $SSH_CONNECTION ]
then
    myssh='%F{cyan} (ssh)%f'
fi

export PS1='$TITLE%B%(1j.%F{cyan}[$myjobs]%f .)%(!.%F{red}.%F{green})%n%f%b%F{green}@%f%B%F{green}%m$myssh%f %F{blue}%(5~,%-1~/.../%3~,%~)%f %(?..%F{red}[%?]%f )%F{blue}%(3L.(i%) .)%#%f%b '
export PS2='%_ %B%F{blue}>%f%b '
export PS3='%B%F{blue}%#%b '
export PS4='%i %B%F{red}+%f%b '

export RPS1='%B%U$VIMODE%u%b'

